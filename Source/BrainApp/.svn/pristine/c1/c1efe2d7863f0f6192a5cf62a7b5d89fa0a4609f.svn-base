library ieee ;
use ieee.std_logic_1164.all ;
use ieee.std_logic_arith.all ;
use ieee.std_logic_unsigned.all ;
use ieee.numeric_std.all;    

entity TesterFpgaFilterElement is
   port (
      RST       : in std_logic;
      CLK       : in std_logic;

      DataIn    : in std_logic_vector(31 downto 0);
      HdrIn     : in std_logic_vector( 3 downto 0);
      OffsetIn  : in std_logic_vector(15 downto 0);
      DataInEn  : in std_logic;
         
      FilterOut : out std_logic;

      ConfigRegsIn   : in std_logic_vector(31 downto 0);
      ConfigRegsOut  : out std_logic_vector(31 downto 0);
      ConfigRegsEn   : in std_logic;
      ConfigRegsLd   : in std_logic;
      ConfigRegsSt   : in std_logic
   );
end TesterFpgaFilterElement;

architecture RTL of TesterFpgaFilterElement is
   type RegisterBank_t is array (0 to 1) of std_logic_vector(31 downto 0);

   -- Reg 0
   -- | 31:24 | 23:16  |  15:8  |   7:0  |
   -- | Curr  | Initial| HdrMax | HdrMin |
   -- | Dcmit | Dcimt  |        |        |
   -- Reg 1
   -- | 31:16          |      15:0       |
   -- | OfstMax        |     OfstMin     |
   signal RegisterBank       : RegisterBank_t;
   signal RegisterBankStatic : RegisterBank_t;
   signal DecDecimationCntr  : std_logic;
begin
   
   -- Filter
   process(CLK, RST)
   begin
      if RST = '1' then
         DecDecimationCntr <= '0';
         FilterOut         <= '0';
      elsif CLK'event and CLK = '1' then
         DecDecimationCntr <= '0';
         FilterOut         <= '0';
         
         if (HdrIn    <= RegisterBank(0)(15 downto  8)) and (HdrIn    >= RegisterBank(0)( 7 downto 0)) and 
            (OffsetIn <= RegisterBank(1)(31 downto 16)) and (OffsetIn >= RegisterBank(1)(15 downto 0)) then
            if (RegisterBank(0)(31 downto 24) = std_logic_vector(to_unsigned(0,8))) then
              FilterOut <= '1';
            else
              DecDecimationCntr <= '1';
            end if;
         end if;
      end if;
   end process;

   -- Config Registers 
   process (RST, CLK)
   begin
      if RST = '1' then
         RegisterBank       <= (others=>(others=>'1'));
         RegisterBankStatic <= (others=>(others=>'1'));
      elsif CLK'event and CLK='1' then
         if (ConfigRegsSt = '1') then
            RegisterBank <= RegisterBankStatic;
         elsif (DecDecimationCntr = '1') then
            if (RegisterBank(0)(31 downto 24) = std_logic_vector(to_unsigned(0,8))) then
              RegisterBank(0)(31 downto 24) <= RegisterBank(0)(23 downto 16);
            else
              RegisterBank(0)(31 downto 24) <= RegisterBank(0)(31 downto 24) - 1;
            end if;
         end if;
         if (ConfigRegsLd = '1') then 
            RegisterBankStatic <= RegisterBank;
         end if;
         if (ConfigRegsEn = '1') then
            RegisterBankStatic(1) <= ConfigRegsIn;
            RegisterBankStatic(0) <= RegisterBankStatic(1);
         end if;
      end if;
   end process;
   ConfigRegsOut <= RegisterBankStatic(0);
   
end RTL;
